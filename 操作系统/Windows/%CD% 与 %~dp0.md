# %CD% 与 %~dp0

`%CD%`：Current Directory, 当前工作目录位置  
`%~dp0`：批次档存在的目录位置

请试着执行下面例子感受两者间的差异：

```dos
@ECHO OFF
CLS

REM ================
REM == 先比较效果 ==
REM ================
ECHO ===============================================
ECHO == [Step1] 看起来没什麽不同, 除了结尾的斜线 ...
ECHO ===============================================
ECHO %%CD%% = %CD%
ECHO %%~dp0 = %~dp0

REM ==========================
REM == 切换工作目录再做比较 ==
REM ==========================
IF NOT EXIST TEMP MKDIR TEMP
CD TEMP
ECHO ===============================================
ECHO == [Step2] 切换工作目录后, 发生差异了!
ECHO ===============================================
ECHO %%CD%% = %CD%
ECHO %%~dp0 = %~dp0
CD ..

:CLEAN
RMDIR /S /Q TEMP
```

上面例子可以明确知道：

1. `%~dp0`表示批次档所存在的位置, 是不会因切换工作目录而改变。
2. `%CD%`表示的是当前工作目录位置, 如果用CD之类的指令切换工作目录, 则%CD%会改变成对应的目录。

关于`%~dp0`其实是`%0`参数的衍伸用法（可以使用`CALL /?`查阅详细说明）

首先执行下面例子先看看结果：

```dos
@ECHO OFF
CLS

ECHO %%0 = %0
ECHO %%~d0 = %~d0
ECHO %%~p0 = %~p0
ECHO %%~n0 = %~n0
ECHO %%~x0 = %~x0
```

从`CALL /?`的说明页面中, 得知在参数`%n (%0, %1, ... %9)`之间加了波浪符号`(~)`与相关的修饰字词后, 就会有扩充的支援。

1. `%0`：第0个参数, 表示批次档本身。
2. `%~d0`：Dirver, 批次档存在的磁碟机代号。
3. `%~p0`：Path, 批次档的路径位置（不包含磁碟机代号）。
4. `%~n0`：批次档的档桉名称。
5. `%~x0`：批次档的副档名。
6. 所有的修饰字词可以溷搭使用, 所以`%~dp0`即是批次档所在的完整路径。
7. 修饰字词还有~f, ~a, ...等, 请参考程式说明页面。

---
via: <http://inpega.blogspot.com/2012/07/cd-dp0.html>
